plugins {
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

swaggerSources {
    quizappApi {
        inputFile = file("../../api/api.yaml")
        code {
            language = 'kotlin'
            configFile = file('config.json')
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "pl.edu.agh.toik.quizapp"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += "${swaggerSources.quizappApi.code.outputDir}/src/main/kotlin/"
        }
    }
}

dependencies {
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.github.arcadefire:nice-spinner:1.4.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.volley:volley:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // swagger dependencies
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.squareup.moshi:moshi-kotlin:1.5.0"
    implementation "com.squareup.moshi:moshi-adapters:1.5.0"
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "org.threeten:threetenbp:1.3.6"
    testImplementation "io.kotlintest:kotlintest:2.0.2"
}

gradle.projectsEvaluated {
    preBuild.dependsOn swaggerSources.quizappApi.code
}
